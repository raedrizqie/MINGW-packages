# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=qtwebkit
pkgbase=mingw-w64-${_realname}-static
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-static"
_pkgver=5.212.0-alpha4
pkgver=${_pkgver//-/}
pkgrel=1
pkgdesc="Webkit module for Qt5 (static) (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/qtwebkit/qtwebkit/wiki"
msys2_repository_url="https://github.com/qtwebkit/qtwebkit"
msys2_references=(
  'aur: qt5-webkit'
  "cpe: cpe:/a:qt:qt5-webkit"
)
license=('spdx:Qt-GPL-exception-1.0 AND GPL-3.0-or-later AND LGPL-2.1-or-later AND LGPL-3.0-only AND GFDL-1.3-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-gperf"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-ruby"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-qt5-tools"
             "${MINGW_PACKAGE_PREFIX}-icu"
             "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
             "${MINGW_PACKAGE_PREFIX}-libpng"
             "${MINGW_PACKAGE_PREFIX}-libxml2"
             "${MINGW_PACKAGE_PREFIX}-libxslt"
             "${MINGW_PACKAGE_PREFIX}-sqlite3")
depends=("${MINGW_PACKAGE_PREFIX}-qt5-static")
source=(https://github.com/annulen/webkit/releases/download/qtwebkit-${_pkgver}/${_realname}-${_pkgver}.tar.xz
        0001-fix-build-with-gcc-14.patch
        0003-qtwebkit-mfence-mingw.patch
        0005-fix-icu-find.patch
        0006-python-output-unix-line-endings.patch
        0008-fix-using-msys-perl.patch
        0011-mingw-posix-layout-files.patch
        0014-disable-asm-win64.patch
        0015-use-proper-import-suffix-mingw.patch
        0016-separate-debug-into-bin.patch
        0202-install-private-headers-and-modules.patch
        0300-fix-generating-module-files.patch
        0501-build-gcc9.patch
        0502-clang-compat.patch
        0503-python3.patch
        0504-qwebinspector-crash-workaround.patch
        0601-missing-include.patch
        0701-ruby-parser-asm-fixes.patch
        0801-suppress-some-warnings.patch
        0802-properly-link-with-system-libs.patch
        0803-c++17-updates.patch
        0804-printing-size_t-correctly.patch
        0805-libxml2-updates.patch
        0806-inspector-codegen-python3.patch
        0807-deprecated-pure-parser.patch
        0808-remove-bogus-defines.patch
        0809-non-literal-format-string.patch
        0810-static-libs-no-export.patch
        0811-remove-user-defined-copy.patch
        0812-final-class-inheritence.patch
        0813-logical-expression-operands.patch
        0814-cachevalidation-string-concatenation.patch
        0815-missing-override.patch
        0816-use-explicit-copy.patch
        0901-minimal-private-libs.patch
        0902-proper-prl-filename-in-static-build.patch
        d92b11fea65364fefa700249bd3340e0cd4c5b31.patch
        6e18844035d9c5e25d770ed817b858c2423d894b.patch
        4e1f209f21c3958e5a8d3a0646dccf9e429ca7e4.patch)
sha256sums=('9ca126da9273664dd23a3ccd0c9bebceb7bb534bddd743db31caf6a5a6d4a9e6'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Np1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${_realname}-${_pkgver}

  apply_patch_with_msg \
    0001-fix-build-with-gcc-14.patch \
    0003-qtwebkit-mfence-mingw.patch \
    0005-fix-icu-find.patch \
    0006-python-output-unix-line-endings.patch \
    0008-fix-using-msys-perl.patch \
    0011-mingw-posix-layout-files.patch \
    0014-disable-asm-win64.patch \
    0015-use-proper-import-suffix-mingw.patch \
    0016-separate-debug-into-bin.patch

  # Upstream patches
  apply_patch_with_msg \
    0202-install-private-headers-and-modules.patch

  apply_patch_with_msg \
    0300-fix-generating-module-files.patch \
    0501-build-gcc9.patch \
    0502-clang-compat.patch \
    0503-python3.patch \
    0504-qwebinspector-crash-workaround.patch \
    0601-missing-include.patch

  # Fix for updated ruby
  apply_patch_with_msg \
    0701-ruby-parser-asm-fixes.patch

  # Fix build with bison 3.7+
  apply_patch_with_msg \
    d92b11fea65364fefa700249bd3340e0cd4c5b31.patch \
    6e18844035d9c5e25d770ed817b858c2423d894b.patch \
    4e1f209f21c3958e5a8d3a0646dccf9e429ca7e4.patch

  apply_patch_with_msg \
    0801-suppress-some-warnings.patch \
    0802-properly-link-with-system-libs.patch \
    0803-c++17-updates.patch \
    0804-printing-size_t-correctly.patch \
    0805-libxml2-updates.patch \
    0806-inspector-codegen-python3.patch \
    0807-deprecated-pure-parser.patch \
    0808-remove-bogus-defines.patch \
    0809-non-literal-format-string.patch \
    0810-static-libs-no-export.patch \
    0811-remove-user-defined-copy.patch \
    0812-final-class-inheritence.patch \
    0813-logical-expression-operands.patch \
    0814-cachevalidation-string-concatenation.patch \
    0815-missing-override.patch \
    0816-use-explicit-copy.patch

  # Add support for static build
  apply_patch_with_msg \
    0901-minimal-private-libs.patch \
    0902-proper-prl-filename-in-static-build.patch
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    _extra_config+=("-DCMAKE_BUILD_TYPE=Debug" "-DSEPARATE_DEBUG_INFO=ON")
  fi

  export PATH="${MINGW_PREFIX}/qt5-static/bin:${PATH}"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  cmake -Wno-dev \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}/qt5-static \
    "${_extra_config[@]}" \
    -DPORT=Qt \
    -DQT_BUNDLED_PNG=ON \
    -DENABLE_API_TESTS=ON \
    -DUSE_THIN_ARCHIVES=OFF \
    -S ${_realname}-${_pkgver} \
    -B build-${MSYSTEM}

  cmake --build build-${MSYSTEM}
}

package() {
  DESTDIR="${pkgdir}" cmake --install build-${MSYSTEM}

  # Remove unused libs (already integrated in libWebCore and libJavaScriptCore)
  rm -f ${pkgdir}${MINGW_PREFIX}/qt5-static/lib/{libANGLESupport,libWTF}.a

  install -Dm755 build-${MSYSTEM}/bin/QtTestBrowser.exe ${pkgdir}${MINGW_PREFIX}/qt5-static/bin/QtTestBrowser.exe
}
